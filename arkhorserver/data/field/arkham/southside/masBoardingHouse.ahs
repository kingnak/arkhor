FieldOption MasMouse_Recruit {
    field: SS_MasBoardingHouse;
    name: "Recruit";
    description: "Instead of having an encounter here, "+
        "you may spend 10 toughness worth of monster trophies, "+
        "2 gate trophies, or 5 toughness worth of monster trophies "+
        "and 1 gate trophy to take 1 Ally of your choice from "+
        "the Ally deck.";
    costs: [ MonsterToughness 10, GateTrophy 2, MonsterToughness 5 + GateTrophy 1 ];
    activate: {{{
        var res = game.context.drawMultipleObjects(Constants.ObjectType.Ally, "Recruit Ally", this.id, 1000, 0, 1);
        if (res.length > 0) {
            var item = res[0];
            CHAR.addToInventory(item);
        }
    }}};
}

ArkhamEncounter MasHouse_Enc1 {
    field: SS_MasBoardingHouse;
    description: "After supper while sitting on the "+
        "porch you strike up a conversation with another "+
        "guest: Ryan Dean, a traveling salesman. You may "+
        "make a <b>Will (+0) check</b>. If you pass, Ryan "+
        "has a deal for you. You may draw either 1 Common "+
        "Item or 1 Unique Item and purchase it at list price. "+
        "If you fail, stay here next turn listening to "+
        "bawdy stories and tall tales.";
    options: ( Option.MasHouse_Enc1, Option.SKIP );
}
AEOption MasHouse_Enc1 {
    name: "Check Ryan's deal";
    baseProperty: DieRoll_Will;
    activate: {{{
        var res = game.context.skillTest("Talk to Ryan", this.id, Constants.Skills.Will, 0, 1);
        if (res) {
            var sel = game.context.selectChoice("Get Common or Unique Item?", this.id, [["C", "Common Item", "Buy a Common Item"],["U", "Unique Item", "Buy a Unique Item"]]);
            var t = (sel == "C") ? Constants.ObjectType.CommonItem : Constants.ObjectType.UniqueItem;
            var o = game.context.drawMultipleObjects(t, "Buy item", this.id, 1, 0, 1000);
            if (o.length <= 0) return;
            o = o[0];
            if (CHAR.pay({type: Constants.Costs.Money, amount: o.price})) {
                CHAR.addToInventory(o);
            } else {
                o.returnToDeck();
            }
        } else {
            game.context.character.delay();
        }
    }}};
}

ArkhamEncounter MasHouse_Enc2 {
    field: SS_MasBoardingHouse;
    description: "Chanting neighbors keep you up all "+
        "night. Pass a <b>Luck (-1) check</b> or lose your "+
        "choice of 1 Stamina or 1 Sanity.";
    options: Option.MasHouse_Enc2;
}
AEOption MasHouse_Enc2 {
    activate: {{{
        var res = game.context.skillTest("Try to sleep", this.id, Constants.Skills.Luck, -1, 1);
        if (!res) {
            var type = game.context.selectChoice("Lose 1 Stamina or Sanity?", this.id, ["Stamina", "Sanity"]);
            if (type == "Stamina") {
                CHAR.damageStamina(1);
            } else {
                CHAR.damageSanity(1);
            }
        }
    }}};
}

ArkhamEncounter {
    field: SS_MasBoardingHouse;
    description: "Ma Mathison serves her special soup at supper. Roll a die and gain that much Stamina.";
    options: {AEOption {
        activate: {{{
            var res = game.context.genericDieRollSum("Eat soup", this.id, 1);
            CHAR.addStamina(res);
        }}};
    }};
}

ArkhamEncounter {
    field: SS_MasBoardingHouse;
    description: "Ma Mathison tells you that the best room on the house is available for the night. If you want, "+
                 "pay $3 to spend the night there. Gain 4 points split between Sanity and Stamina however you choose.";
    options: (Option.SKIP, {AEOption {
        name: "Spend night in room";
        costs: [ Money 3 ];
        activate: {{{
            var amount = 4;
            // Ask for stamina
            var options = [];
            for (var i = 0; i <= amount; ++i) {
                options.push(i);
            }
            var stam = game.context.selectChoice("Heal how much stamina?", this.id, options);
            CHAR.addStamina(stam);
            amount -= stam;

            if (amount <= 0) {
                // No more for sanity
                return;
            }

            // Ask for sanity
            options = [];
            for (var i = 0; i <= amount; ++i) {
                options.push(i);
            }
            var san = game.context.selectChoice("Heal how much sanity?", this.id, options);
            CHAR.addSanity(san);
        }}};
    }});
}

/*
Staring at a painting in one of the rooms, you find yourself drawn into it. Make a Luck (+0) check. If you pass, move to The Dreamlands. Have one encounter there and immediately return here. If you fail, move to the Abyss. Have one encounter there and immediately return here.	Luck +0
The last guest to stay in your room had to leave in a hurry and left something behind. Draw 1 Common Item.
You find a poorly boarded-up passage in the basement that opens into a winding tunnel. If you venture into it, you exit in the Silver Twilight Lodge. Draw 2 cards and encounter one card of your choice, discarding the other.
*/
